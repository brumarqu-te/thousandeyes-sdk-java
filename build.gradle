buildscript {
    repositories {
        mavenCentral()
    }
}

ext {
    apacheCommonsLang3 = "3.14.0"
    googleJsr305Version = "3.0.2"
    mockitoVersion = "3.2.4"
    jacksonDatabindNullableVersion = "0.2.6"
    jacksonVersion = "2.16.1"
    jacocoVersion = "0.8.8"
    junitVersion = "5.10.1"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    group = 'com.thousandeyes.sdk'
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    // Some text from the schema is copy pasted into the source files as UTF-8
    // but the default still seems to be to use platform encoding
    tasks.withType(JavaCompile) {
        configure(options) {
            options.encoding = 'UTF-8'
        }
    }
    javadoc {
        options.encoding = 'UTF-8'
    }

    task execute(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    def jacocoXmlReport = "${project.rootDir}/${project.name}/reports/jacoco/${project.name}.xml"

    jacoco {
        toolVersion = "$jacocoVersion"
    }

    jacocoTestReport {
        sourceSets sourceSets.main
        executionData.from = fileTree(project.buildDir).include("/jacoco/*.exec")
        reports {
            csv.required = false
            html.required = false
            xml.required = true
            xml.outputLocation.set(file(jacocoXmlReport))
        }
    }

    test {
        useJUnitPlatform()
        ignoreFailures true
        finalizedBy jacocoTestReport
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'ThousandEyes Java SDK - ${project.name}'
                    description = 'A set of Java client libraries for the [Thousandeyes v7 API](https://developer.cisco.com/docs/thousandeyes/v7/).'
                    url = 'https://github.com/thousandeyes/thousandeyes-sdk-java'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'ThousandEyes API Team'
                            email = 'api-team@thousandeyes.com'
                            organization = 'Cisco - ThousandEyes'
                            organizationUrl = 'https://www.thousandeyes.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/thousandeyes/thousandeyes-sdk-java.git'
                        developerConnection = 'scm:git:ssh://github.com/thousandeyes/thousandeyes-sdk-java.git'
                        url = 'https://github.com/thousandeyes/thousandeyes-sdk-java/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "CentralPortal"
                url = "https://central.portal.url/repository/maven-releases/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        def signingKeyId = findProperty("signingKeyId")
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}

